PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX schema: <http://schema.org/>
PREFIX cnt: <http://www.w3.org/2011/content#>

SELECT
  ?distribution
  ?title
  ?access
  ?description
  ?standard
  ?standard_title
  ?media_type
  ?media_type_title
  ?documentation_fulltext
  ?download_url
  (REPLACE(REPLACE(STR(?landing_page), "https://fornpunkt.se/data/", ""), "/", "") AS ?slug)
WHERE {
  ?distribution a  dcat:Distribution;
           dct:title ?title ;
           dct:description ?description ;
           dct:accessRights ?access ;
           dcat:landingPage ?landing_page ;
           schema:subjectOf ?documentation .

  ?documentation cnt:chars ?documentation_fulltext .

  OPTIONAL {
    ?distribution dct:conformsTo ?standard .
    ?standard dct:title ?standard_title .
  }

  OPTIONAL {
    ?distribution dcat:mediaType ?media_type .
    ?media_type dct:title ?media_type_title .
  }

  OPTIONAL {
    ?distribution dcat:downloadURL ?download_url .
  }
}
ORDER BY ?title
